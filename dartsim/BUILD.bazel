load(
    "@gz//bazel/skylark:build_defs.bzl",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "gz_configure_header",
    "gz_export_header",
    "gz_include_header",
)
load(
    "@gz//bazel/lint:lint.bzl",
    "add_lint_tests",
)

public_headers = ["include/gz/physics/dartsim/World.hh"]

private_headers = glob(["src/*.hh"])

sources = glob(
    ["src/*.cc"],
    exclude = [
        "src/*_TEST.cc",
        "src/plugin.cc",
    ],
)

cc_library(
    name = "dartsim",
    srcs = sources + private_headers,
    hdrs = public_headers,
    includes = ["include"],
    visibility = GZ_VISIBILITY,
    deps = [
        GZ_ROOT + "common",
        GZ_ROOT + "common/geospatial",
        GZ_ROOT + "common/profiler",
        GZ_ROOT + "math/eigen3",
        GZ_ROOT + "physics",
        GZ_ROOT + "physics:heightmap",
        GZ_ROOT + "physics:mesh",
        GZ_ROOT + "physics:sdf",
        GZ_ROOT + "third_party/dart:simulation",
        GZ_ROOT + "third_party/dart:collision-bullet",
        GZ_ROOT + "third_party/dart:collision-ode",
    ],
)

cc_binary(
    name = "libgz-physics-dartsim-plugin.so",
    srcs = [
        "src/plugin.cc",
    ],
    linkshared = True,
    visibility = GZ_VISIBILITY,
    deps = [
        ":dartsim",
        GZ_ROOT + "plugin:register",
    ],
)

# AddedMassFeatures_TEST must directly link with dart:simulation
cc_test(
    name = "AddedMassFeatures_TEST",
    srcs = ["src/AddedMassFeatures_TEST.cc"] + private_headers + public_headers,  # noqa: 501
    copts = ["-fexceptions"],
    data = [
        "worlds",
        ":libgz-physics-dartsim-plugin.so",
        GZ_ROOT + "physics/test:resources",
    ],
    env = {
        "GZ_BAZEL": "1",
        "GZ_BAZEL_PATH": "physics",
    },
    includes = [
        "include",
        "src",
    ],
    local_defines = [
        "TEST_WORLD_DIR='\"physics/dartsim/worlds/\"'",
        "GZ_PHYSICS_RESOURCE_DIR='\"physics/test/resources/\"'",
        "dartsim_plugin_LIB='\"physics/dartsim/libgz-physics-dartsim-plugin.so\"'",  # noqa: 501
    ],
    deps = [
        GZ_ROOT + "common/geospatial",
        GZ_ROOT + "math/eigen3",
        GZ_ROOT + "physics",
        GZ_ROOT + "physics:heightmap",
        GZ_ROOT + "physics:mesh",
        GZ_ROOT + "physics:sdf",
        GZ_ROOT + "physics/test:common_test",
        GZ_ROOT + "third_party/dart:simulation",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

# Base_TEST directly links against the dartsim library while the others
# load the plugin library via gz-plugin
cc_test(
    name = "Base_TEST",
    srcs = ["src/Base_TEST.cc"] + private_headers + public_headers,
    copts = ["-fexceptions"],
    data = [
        "worlds",
        GZ_ROOT + "physics/test:resources",
    ],
    env = {
        "GZ_BAZEL": "1",
        "GZ_BAZEL_PATH": "physics",
    },
    includes = [
        "include",
        "src",
    ],
    deps = [
        ":dartsim",
        GZ_ROOT + "physics/test:common_test",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

# SDFFeatures_TEST must directly link with dart:simulation
cc_test(
    name = "SDFFeatures_TEST.cc",
    srcs = ["src/SDFFeatures_TEST.cc"] + private_headers + public_headers,
    copts = ["-fexceptions"],
    data = [
        "worlds",
        ":libgz-physics-dartsim-plugin.so",
        GZ_ROOT + "physics/test:resources",
    ],
    env = {
        "GZ_BAZEL": "1",
        "GZ_BAZEL_PATH": "physics",
    },
    includes = [
        "include",
        "src",
    ],
    local_defines = [
        "TEST_WORLD_DIR='\"physics/dartsim/worlds/\"'",
        "GZ_PHYSICS_RESOURCE_DIR='\"physics/test/resources/\"'",
        "dartsim_plugin_LIB='\"physics/dartsim/libgz-physics-dartsim-plugin.so\"'",  # noqa: 501
    ],
    deps = [
        GZ_ROOT + "common/geospatial",
        GZ_ROOT + "math/eigen3",
        GZ_ROOT + "physics",
        GZ_ROOT + "physics:heightmap",
        GZ_ROOT + "physics:mesh",
        GZ_ROOT + "physics:sdf",
        GZ_ROOT + "physics/test:common_test",
        GZ_ROOT + "third_party/dart:simulation",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

test_sources = glob(
    include = ["src/*_TEST.cc"],
    exclude = [
        "src/AddedMassFeatures_TEST.cc",
        "src/Base_TEST.cc",
        "src/SDFFeatures_TEST.cc",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src] + private_headers + public_headers,
    copts = ["-fexceptions"],
    data = [
        "worlds",
        ":libgz-physics-dartsim-plugin.so",
        GZ_ROOT + "physics/test:resources",
    ],
    env = {
        "GZ_BAZEL": "1",
        "GZ_BAZEL_PATH": "physics",
    },
    includes = [
        "include",
        "src",
    ],
    local_defines = [
        "TEST_WORLD_DIR='\"physics/dartsim/worlds/\"'",
        "GZ_PHYSICS_RESOURCE_DIR='\"physics/test/resources/\"'",
        "dartsim_plugin_LIB='\"physics/dartsim/libgz-physics-dartsim-plugin.so\"'",  # noqa: 501
    ],
    deps = [
        GZ_ROOT + "common/geospatial",
        GZ_ROOT + "math/eigen3",
        GZ_ROOT + "physics",
        GZ_ROOT + "physics:heightmap",
        GZ_ROOT + "physics:mesh",
        GZ_ROOT + "physics:sdf",
        GZ_ROOT + "physics/test:common_test",
        GZ_ROOT + "third_party/dart:constraint_headers",
        GZ_ROOT + "third_party/dart:simulation_headers",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]

add_lint_tests()
